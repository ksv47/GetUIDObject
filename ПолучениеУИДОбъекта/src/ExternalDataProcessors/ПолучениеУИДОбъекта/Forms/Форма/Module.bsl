#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокОбъектовМетаданных  = ПолучитьСписокОбъектовМетаданных();
	Для Каждого ЭлементСписка Из СписокОбъектовМетаданных Цикл
		Элементы.ВидМетаданных.СписокВыбора.Добавить(
			ЭлементСписка.Значение, ЭлементСписка.Представление, , ЭлементСписка.Картинка);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидМетаданныхПриИзменении(Элемент)
	
	Если ВидМетаданных <> ПредыдущееЗначениеВидМетаданных Тогда
		ЗаполнитьСписокТипОбъекта();
	КонецЕсли;
	
	ПредыдущееЗначениеВидМетаданных = ВидМетаданных;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Подбор(Команда)
		
	ПараметрыПодбора = Новый Структура;    
    ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Истина);
    ПараметрыПодбора.Вставить("МножественныйВыбор", Истина);
    ПараметрыПодбора.Вставить("РежимВыбора", Истина);
    ПараметрыПодбора.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеПодбора", ЭтаФорма); 
	ОткрытьФорму(ТипОбъекта + ".ФормаВыбора", ПараметрыПодбора, , , , , ОписаниеОповещения);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокТипОбъекта()
	
	СписокОбъектовМетаданных  = ПолучитьСписокОбъектовМетаданных();
	ВыбранныйЭлемент = СписокОбъектовМетаданных.НайтиПоЗначению(ВидМетаданных);
	Картинка = ВыбранныйЭлемент.Картинка;
	
	СписокВыбора = Элементы.ТипОбъекта.СписокВыбора;
	СписокВыбора.Очистить();
	РасширитьСписокВыбора(Метаданные[ВидМетаданных], СписокВыбора, Картинка);
	СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РасширитьСписокВыбора(ВидМетаданных, СписокВыбора, Картинка)
	
	Для Каждого ЭлементСписка Из ВидМетаданных Цикл
		СписокВыбора.Добавить(ЭлементСписка.ПолноеИмя(), ЭлементСписка.Представление(), , Картинка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокОбъектовМетаданных()
	
	СписокОбъектовМетаданных = Новый СписокЗначений;
	СписокОбъектовМетаданных.Добавить("Справочники", "Справочники", , БиблиотекаКартинок.Справочник);
	СписокОбъектовМетаданных.Добавить("Документы", "Документы", , БиблиотекаКартинок.Документ);
	
	СписокОбъектовМетаданных.Добавить("БизнесПроцессы", "Бизнес процессы", , БиблиотекаКартинок.БизнесПроцесс);
	СписокОбъектовМетаданных.Добавить("Задачи", "Задачи", , БиблиотекаКартинок.Задача);
	
	СписокОбъектовМетаданных.Добавить("ПланыВидовРасчета", "Планы видов расчета", , БиблиотекаКартинок.ПланВидовРасчета);
	СписокОбъектовМетаданных.Добавить("ПланыВидовХарактеристик", "Планы видов характеристик", ,
		 БиблиотекаКартинок.ПланВидовХарактеристик);
	СписокОбъектовМетаданных.Добавить("ПланыСчетов", "Планы счетов", , БиблиотекаКартинок.ПланСчетов);

	СписокОбъектовМетаданных.Добавить("Константы", "Константы", , БиблиотекаКартинок.Константа);

	СписокОбъектовМетаданных.Добавить("ПланыОбмена", "Планы обмена", , БиблиотекаКартинок.ПланОбмена);
	
	Возврат СписокОбъектовМетаданных;
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеПодбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЗавершениеПодбораНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗавершениеПодбораНаСервере(РезультатПодбора)
	
	Для каждого ЭлементМассива Из РезультатПодбора Цикл
		НоваяСтрока = СписокОбъектов.Добавить();
		НоваяСтрока.Ссылка = ЭлементМассива;  
		НоваяСтрока.УникальныйИдентификатор = ЭлементМассива.УникальныйИдентификатор();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти